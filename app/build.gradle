apply plugin: 'com.android.application'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

android {
    signingConfigs {
        release {
            keyAlias 'messenger'
            keyPassword 'sasaki570030'
            storeFile file('../keystore.jks')
            storePassword 'sasaki570030'
        }
    }

    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.anotap.messenger"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        warning 'InvalidPackage'
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            multiDexEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            multiDexEnabled true
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def file = output.outputFile
            outputFileName = new File(file.name.replace(".apk", "-"
                    + versionName
                    + ".apk").replace("app", "messengernew"))
        }
    }

    /*applicationVariants.all { variant ->
        def flavor = variant.mergedFlavor
        def versionCode = flavor.versionCode

        def versionName = flavor.versionName
        if (variant.buildType.isDebuggable()) {
            versionCode += 1
            versionName = versionCode + "-d"
        }

        flavor.versionCode = versionCode
        flavor.versionName = versionName
    }*/

    flavorDimensions "type"

    productFlavors {
        full {
            applicationId "com.anotap.messenger"
            dimension "type"
            buildConfigField 'int', 'VK_API_APP_ID', getProps("full_vk_app_id")
            buildConfigField 'String', 'GCM_SENDER_ID', getProps("full_gcm_sender_id")
            buildConfigField 'boolean', 'FULL_APP', 'true'
            buildConfigField 'String', 'SERVICE_TOKEN', getProps("full_vk_service_token")
            buildConfigField 'String', 'VK_CLIENT_SECRET', getProps("full_vk_client_secret")
            buildConfigField 'String', 'YOUTUBE_DEV_KEY', getProps("full_youtube_dev_key")
        }
        lite {
            applicationId "com.anotap.messenger"
            dimension "type"
            buildConfigField 'int', 'VK_API_APP_ID', getProps("lite_vk_app_id")
            buildConfigField 'String', 'GCM_SENDER_ID', getProps("lite_gcm_sender_id")
            buildConfigField 'boolean', 'FULL_APP', 'false'
            buildConfigField 'String', 'SERVICE_TOKEN', getProps("lite_vk_service_token")
            buildConfigField 'String', 'VK_CLIENT_SECRET', getProps("lite_vk_client_secret")
            buildConfigField 'String', 'YOUTUBE_DEV_KEY', getProps("lite_youtube_dev_key")
        }
    }
}

def getProps(String propName) {
    def propsFile = rootProject.file('build-config-fields.properties')

    if (propsFile.exists()) {
        def props = new Properties()

        FileInputStream fis
        try {
            fis = new FileInputStream(propsFile)
            props.load(fis)

            def value = props[propName]
            fis.close()
            return value
        } finally {
            if(fis != null) fis.close()
        }
    } else {
        throw new GradleException('File build-config-fields.properties not found')
    }
}

allprojects {
    repositories {
        maven {
            url "https://jitpack.io"
        }

        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }

        maven {
            url 'https://maven.google.com'
        }
    }
}

ext.supportLibraryVersion = '27.1.0'
ext.firebaseLibraryVersion = '15.0.0'

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:support-core-ui:$supportLibraryVersion"
    implementation "com.android.support:support-vector-drawable:$supportLibraryVersion"
    implementation "com.android.support:support-media-compat:$supportLibraryVersion"
    implementation "com.android.support:customtabs:$supportLibraryVersion"
    implementation 'com.squareup.picasso:picasso:3.0.0-SNAPSHOT'
    implementation "com.google.firebase:firebase-messaging:15.0.2"
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation files('libs/YouTubeAndroidPlayerApi.jar')
    implementation project(':mvpcore')
    implementation 'com.squareup.retrofit2:retrofit:2.2.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.2.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.2.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation "com.google.firebase:firebase-crash:15.0.2"
    implementation "com.google.firebase:firebase-database:15.0.0"
    implementation 'com.google.android.exoplayer:exoplayer:2.7.1'
    implementation "com.android.support:exifinterface:$supportLibraryVersion"
    implementation 'com.android.support:multidex:1.0.3'
}

apply plugin: 'com.google.gms.google-services'